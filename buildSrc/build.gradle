plugins {
	id "java-gradle-plugin"
	id "java"
	id "groovy"
	id 'com.apollographql.apollo' version '2.4.6'
}

sourceCompatibility = JavaVersion.VERSION_17

repositories {
	gradlePluginPortal()
	mavenCentral()
	maven { url 'https://repo.spring.io/milestone' }
}

sourceSets {
	main {
		java {
			srcDirs = []
		}
		groovy {
			srcDirs += ["src/main/java"]
		}
	}
}

gradlePlugin {
	plugins {
		checkAntoraVersion {
			id = "org.springframework.antora.check-version"
			implementationClass = "org.springframework.gradle.antora.AntoraVersionPlugin"
		}
		trang {
			id = "trang"
			implementationClass = "trang.TrangPlugin"
		}
		locks {
			id = "locks"
			implementationClass = "lock.GlobalLockPlugin"
		}
		managementConfiguration {
			id = "io.spring.convention.management-configuration"
			implementationClass = "io.spring.gradle.convention.ManagementConfigurationPlugin"
		}
		updateDependencies {
			id = "org.springframework.security.update-dependencies"
			implementationClass = "org.springframework.security.convention.versions.UpdateDependenciesPlugin"
		}
		updateProjectVersion {
			id = "org.springframework.security.update-version"
			implementationClass = "org.springframework.security.convention.versions.UpdateProjectVersionPlugin"
		}
		sagan {
			id = "org.springframework.security.sagan"
			implementationClass = "org.springframework.gradle.sagan.SaganPlugin"
		}
		githubMilestone {
			id = "org.springframework.github.milestone"
			implementationClass = "org.springframework.gradle.github.milestones.GitHubMilestonePlugin"
		}
		githubChangelog {
			id = "org.springframework.github.changelog"
			implementationClass = "org.springframework.gradle.github.changelog.GitHubChangelogPlugin"
		}
		githubRelease {
			id = "org.springframework.github.release"
			implementationClass = "org.springframework.gradle.github.release.GitHubReleasePlugin"
		}
		s101 {
			id = "s101"
			implementationClass = "s101.S101Plugin"
		}
	}
}

configurations {
	implementation {
		exclude module: 'groovy-all'
	}
}

dependencies {
	api platform(libs.reactor.bom)

	implementation libs.google.gson
	implementation libs.trang
	implementation libs.saxon
	implementation libs.snakeyaml
	implementation localGroovy()

	implementation libs.gradle.nexus.publish.plugin
	implementation 'io.projectreactor:reactor-core'
	implementation libs.gretty
	implementation libs.apollo.runtime
	implementation libs.gradle.versions.plugin
	implementation libs.mustache.java.compiler
	implementation libs.spring.javaformat.gradle.plugin
	implementation libs.nohttp.gradle
	implementation libs.htmlunit
	implementation libs.gradle.ssh.plugin
	implementation libs.build.info.extractor.gradle
	implementation libs.sonarqube.gradle.plugin

	testImplementation platform(libs.junit.bom)
	testImplementation platform(libs.mockito.bom)
	testImplementation "org.junit.jupiter:junit-jupiter-api"
	testImplementation "org.junit.jupiter:junit-jupiter-params"
	testImplementation "org.junit.jupiter:junit-jupiter-engine"
	testImplementation libs.commons.io
	testImplementation libs.assertj.core
	testImplementation 'org.mockito:mockito-core'
	testImplementation 'org.mockito:mockito-junit-jupiter'
	testImplementation libs.mockwebserver
}


tasks.named('test', Test).configure {
	onlyIf { !project.hasProperty("buildSrc.skipTests") }
	useJUnitPlatform()
	jvmArgs(
			'--add-opens', 'java.base/java.lang=ALL-UNNAMED',
			'--add-opens', 'java.base/java.util=ALL-UNNAMED'
	)
}
